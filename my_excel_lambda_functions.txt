## My lambda functions

# Join/merge 2 two-column ranges, based on the matching keys in the first columns
merge = LAMBDA(Table1, Table2, 
    LET(
        join_col1, INDEX(Table1, ,1),  
        join_col2, INDEX(Table2, ,1),  
        filtered_rows, FILTER(Table1, ISNUMBER(MATCH(join_col1, join_col2, 0))), 
        salaries, XLOOKUP(INDEX(filtered_rows, ,1), join_col2, INDEX(Table2, ,2)), 
        final_result, IFERROR(CHOOSE({1,2,3}, INDEX(filtered_rows,,1), INDEX(filtered_rows,,2), salaries), "No Match"),
        final_result
    )
)

# Drop all those columns where all cells are empty and all those rows where all cells are empty	
noEmpty = LAMBDA(range,
LET(
keys, BYROW(range, LAMBDA(rng, TEXTJOIN("",FALSE,rng))),
range2, FILTER(range, keys<>""),
range3, TRANSPOSE(range2),
keys2, BYROW(range3, LAMBDA(rng, TEXTJOIN("",FALSE,rng))),
range4, FILTER(range3, keys2<>""),
TRANSPOSE(range4))
)

# Return a link to Eurostat dataset web GUI for a given dataset code name
linkToEurostatDataset	= LAMBDA(
  dataset_code,
    "https://ec.europa.eu/eurostat/databrowser/view/" &
      UPPER(dataset_code) & "/default/table?lang=en"
)

withoutTopRow	= LAMBDA(range, drop(range, 1, ))

topRow	= LAMBDA(range, take(range, 1, ))

valOfCols = lambda(range_with_column_names, column_names,
		withoutTopRow(selectColumnsByName(range_with_column_names, column_names)))

addNewCols = lambda(range_with_column_names, new_column_names, new_column_values,
		HSTACK(range_with_column_names,
			VSTACK(new_column_names, new_column_values))
		)

# Borrowed from https://www.get-digital-help.com/workaround-for-the-textsplit-function-lambda-function
textsplitArray = LAMBDA(range, delimiter, REDUCE(,range,LAMBDA(x,y,VSTACK(TEXTSPLIT(x, delimiter),TEXTSPLIT(y, delimiter)))))

# From a given range with column names in its top row, return the columns according to the provided names
selectColumnsByName	= Lambda(range,
                             names_of_columns_to_select,
                             let(
                               indexes,
                               MATCH(names_of_columns_to_select, topRow(range), 0),
                               CHOOSECOLS(range, indexes)
                             ))

repeat = LAMBDA(value,count,
				IF(SEQUENCE(count, 1) <= count, value, ""))

# Repeat a range n times and stack copies horizontally
repeatRangeHorizontally	= LAMBDA(data_range, n,
                                 CHOOSE(SEQUENCE(1, COLUMNS(data_range) *
                                                                  n),
                                        data_range))

# Repeat a range n times and stack copies vertically
repeatRangeVertically	= LAMBDA(data_range, n,
                               INDEX(data_range,
                                     MOD(SEQUENCE(ROWS(data_range) * n) - 1,
                                         ROWS(data_range)) + 1,
                                     SEQUENCE(1, COLUMNS(data_range))))

# Similar to R's reshape/melt or Stata's reshape long:
# Reshape a range with column names in its top row to a
# "flatfile" type of multi-row format
reshapeToLong	= lambda(
  range_with_column_names,
  names_of_id_columns,
  names_of_columns_with_values,
  name_of_new_value_column,
  name_of_new_id_column,
  let(
    id_data,
    withoutTopRow(selectColumnsByName(range_with_column_names, names_of_id_columns)),
    val_data,
    withoutTopRow(selectColumnsByName(range_with_column_names, names_of_columns_with_values)),
    vstack(
      hstack(
        name_of_new_value_column,
        name_of_new_id_column,
        names_of_id_columns
      ),
      hstack(
        TOCOL(val_data, , TRUE),
        TOCOL(repeat(names_of_columns_with_values , ROWS(id_data))
              ,, TRUE),
        repeatRangeVertically(id_data, columns(
          torow(names_of_columns_with_values, , TRUE)
        ))
      )
    )
  )
)

# Similar to R's reshape/cast/dcast or Stata's reshape wide:
# Reshape a range with column names in its top row to a
# "wide" type of multi-column format
reshapeToWide	= lambda(
  range_with_column_names,
  names_of_id_columns,
  name_of_column_with_values,
  name_of_column_with_ids_to_be_used_as_new_columns,
  let(
    id_data,
    withoutTopRow(selectColumnsByName(range_with_column_names, names_of_id_columns)),
    val_data,
    withoutTopRow(selectColumnsByName(range_with_column_names, name_of_column_with_values)),
	ID_CODE,
	BYROW(id_data, LAMBDA(x,TEXTJOIN("",FALSE,x))),
	uniq_id_data,
	UNIQUE(id_data),
	uniq_ID_CODE,
	BYROW(uniq_id_data,LAMBDA(x,TEXTJOIN("",FALSE,x))),
	NEW_COL_id_column,
	withoutTopRow(selectColumnsByName(range_with_column_names, name_of_column_with_ids_to_be_used_as_new_columns)),
	new_col_names,
	UNIQUE(NEW_COL_id_column),
	three_NAs,
	TRANSPOSE(repeat(NA(),3)),
	mydf,
	HSTACK(NEW_COL_id_column, ID_CODE,val_data),
	REDUCE(VSTACK(names_of_id_columns, uniq_id_data),
		new_col_names,
		LAMBDA(prev_df,x,
			LET(filtered_df, FILTER(mydf, INDEX(mydf,,1)=x, three_NAs),
				filtered_df2, REDUCE({"","",""}, uniq_ID_CODE,
								LAMBDA(prev_df2,y,VSTACK(prev_df2,FILTER(filtered_df,INDEX(filtered_df,,2)=y, three_NAs)))),
				filtered_df3, DROP(filtered_df2,1),
				new_col, VSTACK(x,INDEX(filtered_df3,,3)),
				HSTACK(prev_df,new_col))))
  )
)

# Similar to R's `diff` function for the difference of sets
setdiff	= LAMBDA(Range1, Range2, UNIQUE(FILTER(Range1, ISNA(
  MATCH(Range1, Range2, 0)
))))

# Similar to R's `intersect` function for the intersection of sets
intersect	= lambda(Range1, Range2, UNIQUE(FILTER(Range1, ISNUMBER(
  MATCH(Range1, Range2, 0)
))))

# Evaluate R code in a running localhost Plumber server
# see https://gist.github.com/alekrutkowski/c92c1d8f1c7573a5f5f02e456cbbe3ab
evalRviaPlumber	= LAMBDA(
  rCode,
  LET(
    serverUrl,
    "http://localhost:9000/echo?msg=",
    escapedCode,
    ENCODEURL(rCode),
    rawResult,
    WEBSERVICE(serverUrl & escapedCode),
    rawResultNoSquareBracketAndQuote,
    MID(rawResult, 3, LEN(rawResult) - 4),
    textsplit(rawResultNoSquareBracketAndQuote, "\t", "\r\n")
  )
)

# Transform Excel range into R's vector literal e.g. c(1,2,3) or c('ab','cd')
# and put it in a string
asRvectorLiteral	= lambda(range,
                          let(
                            quotedIfString,
                            IF(
                              ISNUMBER(range),
                              range,
                              "'" & SUBSTITUTE(range, "'", char(92) & "'") & "'"
                            ),
                            joinedString,
                            TEXTJOIN(",", FALSE, quotedIfString),
                            "c(" & joinedString & ")"
                          ))

# Transform Excel range into R's matrix literal
# and put it in a string
asRmatrixLiteral	= lambda(range,
                          "matrix(" &
                            asRvectorLiteral(range) & 
                            ",ncol=" & columns(range) & ",byrow=TRUE)")

# Transform Excel range into R's data.frame literal
# and put it in a string
asRdataframeLiteral	= lambda(
  range_with_column_names,
  LET(
    header,
    "`" & topRow(range) & "`=",
    body,
    BYCOL(withoutTopRow(range_with_column_names), asRvectorLiteral),
    joined,
    TEXTJOIN(",", FALSE, header & body),
    "data.frame(" &
      joined & ",check.names=FALSE)"
  )
)
